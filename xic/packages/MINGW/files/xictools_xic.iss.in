
[Setup]

AppName=xic
AppVerName=xic-VERSION
AppVersion=VERSION
AppPublisher=Whiteley Research Inc.
AppPublisherURL=http://wrcad.com
AppMutex=xicMutex

OutputBaseFilename=APPNAME-OSNAME-VERSION-ARCH

Compression=bzip

DefaultDirName=c:\usr\local
DefaultGroupName=XicTools
DirExistsWarning=no

PrivilegesRequired=admin

UninstallFilesDir={app}\TOOLROOT\xic.current\uninstall
UninstallDisplayIcon={app}\TOOLROOT\xic.current\bin\xic.exe

[Files]

Source: "..\root\usr\local\TOOLROOT\xic.current\bin\preinstall.bat"; DestDir: "{app}\TOOLROOT\xic.current\bin";
Source: "..\root\usr\local\TOOLROOT\xic.current\bin\postinstall.bat"; DestDir: "{app}\TOOLROOT\xic.current\bin"; BeforeInstall: DoBeforeInstall
Source: "..\root\usr\local\TOOLROOT\xic.current\bin\GTK2\xic.exe"; DestDir: "{app}\TOOLROOT\xic.current\bin\GTK2";
Source: "..\root\usr\local\TOOLROOT\xic.current\bin\GTK2\xic.dll"; DestDir: "{app}\TOOLROOT\xic.current\bin\GTK2";
Source: "..\root\usr\local\TOOLROOT\xic.current\bin\QT6\xic.exe"; DestDir: "{app}\TOOLROOT\xic.current\bin\QT6";
Source: "..\root\usr\local\TOOLROOT\xic.current\bin\QT6\xic.dll"; DestDir: "{app}\TOOLROOT\xic.current\bin\QT6";
Source: "..\root\usr\local\TOOLROOT\xic.current\bin\wrdecode.exe"; DestDir: "{app}\TOOLROOT\xic.current\bin";
Source: "..\root\usr\local\TOOLROOT\xic.current\bin\wrencode.exe"; DestDir: "{app}\TOOLROOT\xic.current\bin";
Source: "..\root\usr\local\TOOLROOT\xic.current\bin\wrsetpass.exe"; DestDir: "{app}\TOOLROOT\xic.current\bin"; AfterInstall: DoAfterInstall

Source: "..\root\usr\local\TOOLROOT\xic.current\examples\*"; DestDir: "{app}\TOOLROOT\xic.current\examples"
Source: "..\root\usr\local\TOOLROOT\xic.current\examples\PCells\*"; DestDir: "{app}\TOOLROOT\xic.current\examples\PCells"
Source: "..\root\usr\local\TOOLROOT\xic.current\examples\memchip_example\*"; DestDir: "{app}\TOOLROOT\xic.current\examples\memchip_example"

Source: "..\root\usr\local\TOOLROOT\xic.current\help\*"; DestDir: "{app}\TOOLROOT\xic.current\help"
Source: "..\root\usr\local\TOOLROOT\xic.current\help\screenshots\*"; DestDir: "{app}\TOOLROOT\xic.current\help\screenshots"

Source: "..\root\usr\local\TOOLROOT\xic.current\startup\*"; DestDir: "{app}\TOOLROOT\xic.current\startup"
Source: "..\root\usr\local\TOOLROOT\xic.current\startup\xic_tech.scmos"; DestDir: "{app}\TOOLROOT\xic.current\startup"; DestName: "xic_tech"

Source: "..\root\usr\local\TOOLROOT\xic.current\scripts\*"; DestDir: "{app}\TOOLROOT\xic.current\scripts"
Source: "..\root\usr\local\TOOLROOT\xic.current\scrkit\*"; DestDir: "{app}\TOOLROOT\xic.current\scrkit"

Source: "..\root\usr\local\TOOLROOT\xic.current\docs\xic*"; DestDir: "{app}\TOOLROOT\xic.current\docs";
Source: "..\root\usr\local\TOOLROOT\xic.current\docs\README"; DestDir: "{app}\TOOLROOT\xic.current\docs"
Source: "..\root\usr\local\TOOLROOT\xic.current\docs\MSWINFO.TXT"; DestDir: "{app}\TOOLROOT\xic.current\docs"

[Icons]

Name: "{group}\xic"; Filename: "{app}\TOOLROOT\xic.current\bin\xic.bat"; WorkingDir: "c:\"; IconFilename: "{app}\TOOLROOT\xic.current\bin\xic.exe"

[code]

#include "..\..\..\xt_base\packages\files\inno_uninstall"

function InitializeSetup(): Boolean;
begin
    Result := True;
end;

procedure DoBeforeInstall();
var
    resultCode:  Integer;
begin
    Exec(ExpandConstant('{app}\TOOLROOT\xic.current\bin\preinstall.bat'),
        '', ExpandConstant('{app}\TOOLROOT'), SW_HIDE, ewWaitUntilTerminated,
        resultCode);
end;

procedure DoAfterInstall();
var
    resultCode:  Integer;
begin
    Exec(ExpandConstant('{app}\TOOLROOT\xic.current\bin\postinstall.bat'),
        '', ExpandConstant('{app}\TOOLROOT'), SW_HIDE, ewWaitUntilTerminated,
        resultCode);
end;

function InitializeUninstall(): Boolean;
begin
    DoBeforeInstall();
    Result := True;
end;

