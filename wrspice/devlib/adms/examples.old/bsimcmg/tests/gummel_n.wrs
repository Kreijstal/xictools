*Sample netlist for BSIM-MG 
*Drain current symmetry for nmos

*.option abstol=1e-6 reltol=1e-6 post ingold

*.hdl "bsimcmg.va"
.include "modelcard.nmos"

* --- Voltage Sources ---
vdrain drain 0 dc=0
esource source 0 drain 0 -1
vgate gate  0 dc=1.0
vbulk bulk 0 dc=0.0


* --- Transistor ---
*X1 drain gate source bulk nmos1 TFIN=15n L=30n NFIN=10 NRS=1 NRD=1
m1 drain gate source bulk nmos1 TFIN=15n L=30n NFIN=10 NRS=1 NRD=1

* --- DC Analysis ---
.dc vdrain -0.1 0.1 0.001 vgate 0.0 1.0 0.2
*.probe dc ids=par'-i(vdrain)'
*.probe dc gx=deriv(ids)
*.probe dc gx2=deriv(gx)
*.probe dc gx3=deriv(gx2)
*.probe dc gx4=deriv(gx3)
*.print dc par'ids' par'gx' par'gx2' par'gx3' par 'gx4'

.control
load -c5+2 gummel_n.lis
run
let curplot.ids = -i(vdrain)
plot ysep ids prev.column_1
let curplot.gx = deriv(ids)
# Clip off garbage at endpoints.  Hspice doesn't do differentiation
# very well, note offset and bad values requiring clipping.
plot ysep xlimit="-90m 90m" gx prev.column_2
let curplot.gx2 = deriv(gx)
plot ysep xlimit="-90m 90m" gx2 prev.column_3
let curplot.gx3 = deriv(gx2)
plot ysep xlimit="-90m 90m" gx3 prev.column_4
let curplot.gx4 = deriv(gx3)
plot ysep xlimit="-90m 90m" gx4 prev.column_6
.endc
.end
